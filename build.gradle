/*
 *
 *  Copyright (C) 2010 JFrog Ltd.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 * /
 */

buildscript {
    repositories {
        maven {
            url "http://javaposse.artifactoryonline.com/javaposse/plugins-releases"
        }
        dependencies {
            classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '2.0.12')
        }
    }
}

allprojects {
    group = 'org.jzy3d'
    version = property('jzy3d-version')
    apply plugin: 'idea'
    apply plugin: 'artifactory'
}

artifactory {
    contextUrl = 'http://javaposse.artifactoryonline.com/javaposse'
    publish {
        repository {
            repoKey = 'jogl-local'
            if (project.hasProperty('javaposse_user')) {
                username = "${javaposse_user}"
                password = "${javaposse_password}"
            }
        }
        defaults {
            publishConfigs('extraPublish', 'archives')
            publishIvy = false
            publishPom = true
        }
    }
    resolve {
        repository {
            repoKey = 'jogl-libs'
        }
    }
}

artifactoryPublish.skip = true

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    idea {
        project {
            javaVersion = '1.6'
        }
    }

    configurations {
        extraPublish
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        source = sourceSets.main.java
    }

    artifacts {
        extraPublish sourceJar
    }

    artifactoryPublish {
        dependsOn build
    }

    uploadArchives {
        uploadDescriptor = true
    }
}

project('jzy3d-api') {
    description = 'jzy3d API Project'

    dependencies {
        compile(group: 'org.convexhull', name: 'convexhull', version: '1.0')
        compile(group: 'net.sf.opencsv', name: 'opencsv', version: '2.3')
        compile(group: 'com.jogamp', name: 'jogl-all', version: '2.0')
        compile(group: 'com.jogamp', name: 'gluegen', version: '2.0', classifier: 'rt')
        // TODO: Include the good SWT depending on the OS and also all the above plus OS classifier
        //compile(group: 'org.eclipse.swt.gtk.linux', name: 'x86', version: '3.3.0-v3346')
        compile(group: 'org.eclipse.swt.gtk.linux', name: 'x86_64', version: '3.3.0-v3346')
        //compile(group: 'org.eclipse.swt.carbon', name: 'macosx', version: '3.3.0-v3346')
        //compile(group: 'org.eclipse.swt.win32.win32', name: 'x86', version: '3.3.0-v3346')
    }
}

project('jzy3d-bridge') {
    description = 'jzy3d Bridge to windowing systems Project'

    dependencies {
        compile project(':jzy3d-api')
    }
}

project('jzy3d-demos') {
    description = 'jzy3d Demos Project'

    dependencies {
        compile project(':jzy3d-bridge')
        runtime(group: 'com.jogamp', name: 'jogl-all', version: '2.0', classifier: 'linux64')
        runtime(group: 'com.jogamp', name: 'gluegen', version: '2.0', classifier: 'rt-linux64')
    }
}



